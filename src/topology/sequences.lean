/-
Copyright (c) 2018 Jan-David Salchow. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jan-David Salchow, Patrick Massot
-/

import topology.basic
import topology.bases
import topology.subset_properties

/-!
# Sequences in topological spaces

In this file we define sequences in topological spaces and show how they are related to
filters and the topology. In particular, we
* associate a filter with a sequence and prove equivalence of convergence of the two,
* define the sequential closure of a set and prove that it's contained in the closure,
* define a type class "sequential_space" in which closure and sequential closure agree,
* define sequential continuity and show that it coincides with continuity in sequential spaces,
* provide an instance that shows that every first-countable (and in particular metric) space is a sequential space.

# TODO
* Sequential compactness should be handled here.
-/

open set filter
open_locale topological_space

variables {Î± : Type*} {Î² : Type*}

local notation f ` âŸ¶ ` limit := tendsto f at_top (ð“ limit)

/-! ### Statements about sequences in general topological spaces. -/
section topological_space
variables [topological_space Î±] [topological_space Î²]

/-- A sequence converges in the sence of topological spaces iff the associated statement for filter
holds. -/
@[nolint ge_or_gt] -- see Note [nolint_ge]
lemma topological_space.seq_tendsto_iff {x : â„• â†’ Î±} {limit : Î±} :
  tendsto x at_top (ð“ limit) â†”
    âˆ€ U : set Î±, limit âˆˆ U â†’ is_open U â†’ âˆƒ n0 : â„•, âˆ€ n â‰¥ n0, (x n) âˆˆ U :=
(at_top_basis.tendsto_iff (nhds_basis_opens limit)).trans $
  by simp only [and_imp, exists_prop, true_and, set.mem_Ici, ge_iff_le, id]

/-- The sequential closure of a subset M âŠ† Î± of a topological space Î± is
the set of all p âˆˆ Î± which arise as limit of sequences in M. -/
def sequential_closure (M : set Î±) : set Î± :=
{p | âˆƒ x : â„• â†’ Î±, (âˆ€ n : â„•, x n âˆˆ M) âˆ§ (x âŸ¶ p)}

lemma subset_sequential_closure (M : set Î±) : M âŠ† sequential_closure M :=
assume p (_ : p âˆˆ M), show p âˆˆ sequential_closure M, from
  âŸ¨Î» n, p, assume n, â€¹p âˆˆ Mâ€º, tendsto_const_nhdsâŸ©

/-- A set `s` is sequentially closed if for any converging sequence `x n` of elements of `s`,
the limit belongs to `s` as well. -/
def is_seq_closed (s : set Î±) : Prop := s = sequential_closure s

/-- A convenience lemma for showing that a set is sequentially closed. -/
lemma is_seq_closed_of_def {A : set Î±}
  (h : âˆ€(x : â„• â†’ Î±) (p : Î±), (âˆ€ n : â„•, x n âˆˆ A) â†’ (x âŸ¶ p) â†’ p âˆˆ A) : is_seq_closed A :=
show A = sequential_closure A, from subset.antisymm
  (subset_sequential_closure A)
  (show âˆ€ p, p âˆˆ sequential_closure A â†’ p âˆˆ A, from
    (assume p âŸ¨x, _, _âŸ©, show p âˆˆ A, from h x p â€¹âˆ€ n : â„•, ((x n) âˆˆ A)â€º â€¹(x âŸ¶ p)â€º))

/-- The sequential closure of a set is contained in the closure of that set.
The converse is not true. -/
lemma sequential_closure_subset_closure (M : set Î±) : sequential_closure M âŠ† closure M :=
assume p âŸ¨x, xM, xpâŸ©,
mem_closure_of_tendsto at_top_ne_bot xp (univ_mem_sets' xM)

/-- A set is sequentially closed if it is closed. -/
lemma is_seq_closed_of_is_closed (M : set Î±) (_ : is_closed M) : is_seq_closed M :=
suffices sequential_closure M âŠ† M, from
  set.eq_of_subset_of_subset (subset_sequential_closure M) this,
calc sequential_closure M âŠ† closure M : sequential_closure_subset_closure M
  ... = M : closure_eq_of_is_closed â€¹is_closed Mâ€º

/-- The limit of a convergent sequence in a sequentially closed set is in that set.-/
lemma mem_of_is_seq_closed {A : set Î±} (_ : is_seq_closed A) {x : â„• â†’ Î±}
  (_ : âˆ€ n, x n âˆˆ A) {limit : Î±} (_ : (x âŸ¶ limit)) : limit âˆˆ A :=
have limit âˆˆ sequential_closure A, from
  show âˆƒ x : â„• â†’ Î±, (âˆ€ n : â„•, x n âˆˆ A) âˆ§ (x âŸ¶ limit), from âŸ¨x, â€¹âˆ€ n, x n âˆˆ Aâ€º, â€¹(x âŸ¶ limit)â€ºâŸ©,
eq.subst (eq.symm â€¹is_seq_closed Aâ€º) â€¹limit âˆˆ sequential_closure Aâ€º

/-- The limit of a convergent sequence in a closed set is in that set.-/
lemma mem_of_is_closed_sequential {A : set Î±} (_ : is_closed A) {x : â„• â†’ Î±}
  (_ : âˆ€ n, x n âˆˆ A) {limit : Î±} (_ : x âŸ¶ limit) : limit âˆˆ A :=
mem_of_is_seq_closed (is_seq_closed_of_is_closed A â€¹is_closed Aâ€º) â€¹âˆ€ n, x n âˆˆ Aâ€º â€¹(x âŸ¶ limit)â€º

/-- A sequential space is a space in which 'sequences are enough to probe the topology'. This can be
 formalised by demanding that the sequential closure and the closure coincide. The following
 statements show that other topological properties can be deduced from sequences in sequential
 spaces. -/
class sequential_space (Î± : Type*) [topological_space Î±] : Prop :=
(sequential_closure_eq_closure : âˆ€ M : set Î±, sequential_closure M = closure M)

/-- In a sequential space, a set is closed iff it's sequentially closed. -/
lemma is_seq_closed_iff_is_closed [sequential_space Î±] {M : set Î±} :
  is_seq_closed M â†” is_closed M :=
iff.intro
  (assume _, closure_eq_iff_is_closed.mp (eq.symm
    (calc M = sequential_closure M : by assumption
        ... = closure M            : sequential_space.sequential_closure_eq_closure M)))
  (is_seq_closed_of_is_closed M)

/-- In a sequential space, a point belongs to the closure of a set iff it is a limit of a sequence
taking values in this set. -/
lemma mem_closure_iff_seq_limit [sequential_space Î±] {s : set Î±} {a : Î±} :
  a âˆˆ closure s â†” âˆƒ x : â„• â†’ Î±, (âˆ€ n : â„•, x n âˆˆ s) âˆ§ (x âŸ¶ a) :=
by { rw â† sequential_space.sequential_closure_eq_closure, exact iff.rfl }

/-- A function between topological spaces is sequentially continuous if it commutes with limit of
 convergent sequences. -/
def sequentially_continuous (f : Î± â†’ Î²) : Prop :=
âˆ€ (x : â„• â†’ Î±), âˆ€ {limit : Î±}, (x âŸ¶ limit) â†’ (fâˆ˜x âŸ¶ f limit)

/- A continuous function is sequentially continuous. -/
lemma continuous.to_sequentially_continuous {f : Î± â†’ Î²} (_ : continuous f) :
  sequentially_continuous f :=
assume x limit (_ : x âŸ¶ limit),
have tendsto f (ð“ limit) (ð“ (f limit)), from continuous.tendsto â€¹continuous fâ€º limit,
show (f âˆ˜ x) âŸ¶ (f limit), from tendsto.comp this â€¹(x âŸ¶ limit)â€º

/-- In a sequential space, continuity and sequential continuity coincide. -/
lemma continuous_iff_sequentially_continuous {f : Î± â†’ Î²} [sequential_space Î±] :
  continuous f â†” sequentially_continuous f :=
iff.intro
  (assume _, â€¹continuous fâ€º.to_sequentially_continuous)
  (assume : sequentially_continuous f, show continuous f, from
    suffices h : âˆ€ {A : set Î²}, is_closed A â†’ is_seq_closed (f â»Â¹' A), from
      continuous_iff_is_closed.mpr (assume A _, is_seq_closed_iff_is_closed.mp $ h â€¹is_closed Aâ€º),
    assume A (_ : is_closed A),
      is_seq_closed_of_def $
        assume (x : â„• â†’ Î±) p (_ : âˆ€ n, f (x n) âˆˆ A) (_ : x âŸ¶ p),
        have (f âˆ˜ x) âŸ¶ (f p), from â€¹sequentially_continuous fâ€º x â€¹(x âŸ¶ p)â€º,
        show f p âˆˆ A, from
          mem_of_is_closed_sequential â€¹is_closed Aâ€º â€¹âˆ€ n, f (x n) âˆˆ Aâ€º â€¹(fâˆ˜x âŸ¶ f p)â€º)

end topological_space

namespace topological_space

namespace first_countable_topology

/-- Every first-countable space is sequential. -/
@[priority 100] -- see Note [lower instance priority]
instance [topological_space Î±] [first_countable_topology Î±] : sequential_space Î± :=
âŸ¨show âˆ€ M, sequential_closure M = closure M, from assume M,
  suffices closure M âŠ† sequential_closure M,
    from set.subset.antisymm (sequential_closure_subset_closure M) this,
  -- For every p âˆˆ closure M, we need to construct a sequence x in M that converges to p:
  assume (p : Î±) (hp : p âˆˆ closure M),
  -- Since we are in a first-countable space, there exists a monotonically decreasing
  -- sequence g of sets generating the neighborhood filter around p:
  exists.elim (mono_seq_of_has_countable_basis _
    (nhds_generated_countable p)) $ assume g âŸ¨gmon, gbasisâŸ©,
  -- (g i) is a neighborhood of p and hence intersects M.
  -- Via choice we obtain the sequence x such that (x i).val âˆˆ g i âˆ© M:
  have x : Î  i, g i âˆ© M,
  { rw mem_closure_iff_nhds at hp,
    intro i, apply classical.indefinite_description,
    apply hp, rw gbasis, rw â† le_principal_iff, apply infi_le_of_le i _, apply le_refl _ },
  -- It remains to show that x converges to p. Intuitively this is the case
  -- because x i âˆˆ g i, and the g i get "arbitrarily small" around p. Formally:
  have gssnhds : âˆ€ s âˆˆ ð“ p, âˆƒ i, g i âŠ† s,
  { intro s, rw gbasis, rw mem_infi,
    { simp, intros i hi, use i, assumption },
    { apply directed_of_mono, intros, apply principal_mono.mpr, apply gmon, assumption },
    { apply_instance } },
  -- For the sequence (x i) we can now show that a) it lies in M, and b) converges to p.
  âŸ¨Î» i, (x i).val, by intro i; simp [(x i).property.right],
    begin
      rw tendsto_at_top', intros s nhdss,
      rcases gssnhds s nhdss with âŸ¨i, hiâŸ©,
      use i, intros j hij, apply hi, apply gmon _ _ hij,
      simp [(x j).property.left]
    endâŸ©âŸ©

end first_countable_topology

end topological_space

@[nolint ge_or_gt] -- see Note [nolint_ge]
lemma filter.map_at_top_inf_ne_bot_iff {Î± : Type*} [semilattice_sup Î±] [nonempty Î±] {Î² : Type*} {F : filter Î²} {u : Î± â†’ Î²} :
  (map u at_top) âŠ“ F â‰  âŠ¥ â†” âˆ€ U âˆˆ F, âˆ€ N, âˆƒ n â‰¥ N, u n âˆˆ U :=
by simp_rw [inf_ne_bot_iff_frequently_right, frequently_map, frequently_at_top] ; trivial

lemma extraction_of_frequently_at_top' {P : â„• â†’ Prop} (h : âˆ€ N, âˆƒ n > N, P n) :
  âˆƒ Ï† : â„• â†’ â„•, strict_mono Ï† âˆ§ âˆ€ n, P (Ï† n) :=
begin
  choose u hu using h,
  cases forall_and_distrib.mp hu with hu hu',
  exact âŸ¨u âˆ˜ (nat.rec 0 (Î» n v, u v)), strict_mono.nat (Î» n, hu _), Î» n, hu' _âŸ©,
end

lemma extraction_of_frequently_at_top {P : â„• â†’ Prop} (h : âˆƒá¶  n in at_top, P n) :
  âˆƒ Ï† : â„• â†’ â„•, strict_mono Ï† âˆ§ âˆ€ n, P (Ï† n) :=
begin
  rw frequently_at_top' at h,
  exact extraction_of_frequently_at_top' h,
end

--- High scores

@[nolint ge_or_gt] -- see Note [nolint_ge]
lemma exists_le_of_tendsto_at_top {Î± : Type*} [decidable_linear_order Î±]
  {Î² : Type*} [preorder Î²] {u : Î± â†’ Î²} (h : tendsto u at_top at_top) :
âˆ€ a b, âˆƒ a' â‰¥ a, b â‰¤ u a' :=
begin
  intros a b,
  rw tendsto_at_top at h,
  haveI : nonempty Î± := âŸ¨aâŸ©,
  cases mem_at_top_sets.mp (h b) with a' ha',
  exact âŸ¨max a a', le_max_left _ _, ha' _ $ le_max_right _ _âŸ©,
end

@[nolint ge_or_gt] -- see Note [nolint_ge]
lemma exists_lt_of_tendsto_at_top {Î± : Type*} [decidable_linear_order Î±]
  {Î² : Type*} [preorder Î²] [no_top_order Î²] {u : Î± â†’ Î²} (h : tendsto u at_top at_top) :
âˆ€ a b, âˆƒ a' â‰¥ a, b < u a' :=
begin
  intros a b,
  cases no_top b with b' hb',
  rcases exists_le_of_tendsto_at_top h a b' with âŸ¨a', ha', ha''âŸ©,
  exact âŸ¨a', ha', lt_of_lt_of_le hb' ha''âŸ©
end

@[nolint ge_or_gt] -- see Note [nolint_ge]
lemma high_scores {Î² : Type*} [decidable_linear_order Î²] [no_top_order Î²] {u : â„• â†’ Î²}
  (hu : tendsto u at_top at_top) : âˆ€ N, âˆƒ n â‰¥ N, âˆ€ k < n, u k < u n :=
begin
  intros N,
  let A := finset.image u (finset.range $ N+1), -- A = {u 0, ..., u N}
  have Ane : A.nonempty,
    from âŸ¨u 0, finset.mem_image_of_mem _ (finset.mem_range.mpr $ nat.zero_lt_succ _)âŸ©,
  let M := finset.max' A Ane,
  have ex : âˆƒ n â‰¥ N, M < u n,
    from exists_lt_of_tendsto_at_top hu _ _,
  obtain âŸ¨n, hnN, hnM, hn_minâŸ© : âˆƒ n, N â‰¤ n âˆ§ M < u n âˆ§ âˆ€ k, N â‰¤ k â†’ k < n â†’ u k â‰¤ M,
  { use nat.find ex,
    rw â† and_assoc,
    split,
    { simpa using nat.find_spec ex },
    { intros k hk hk',
      simpa [hk] using nat.find_min ex hk' } },
  use [n, hnN],
  intros k hk,
  by_cases H : k â‰¤ N,
  { have : u k âˆˆ A,
      from finset.mem_image_of_mem _ (finset.mem_range.mpr $ nat.lt_succ_of_le H),
    have : u k â‰¤ M,
      from finset.le_max' A Ane (u k) this,
    exact lt_of_le_of_lt this hnM },
  { push_neg at H,
    calc u k â‰¤ M   : hn_min k (le_of_lt H) hk
         ... < u n : hnM },
end

lemma frequently_high_scores {Î² : Type*} [decidable_linear_order Î²] [no_top_order Î²] {u : â„• â†’ Î²}
  (hu : tendsto u at_top at_top) : âˆƒá¶  n in at_top, âˆ€ k < n, u k < u n :=
by simpa [frequently_at_top] using high_scores hu

lemma strict_mono_subseq_of_tendsto_at_top
  {Î² : Type*} [decidable_linear_order Î²] [no_top_order Î²]
  {u : â„• â†’ Î²} (hu : tendsto u at_top at_top) :
  âˆƒ Ï† : â„• â†’ â„•, strict_mono Ï† âˆ§ strict_mono (u âˆ˜ Ï†) :=
let âŸ¨Ï†, h, h'âŸ© := extraction_of_frequently_at_top (frequently_high_scores hu) in
âŸ¨Ï†, h, Î» n m hnm, h' m _ (h hnm)âŸ©

lemma strict_mono_subseq_of_id_le {u : â„• â†’ â„•} (hu : âˆ€ n, n â‰¤ u n) :
  âˆƒ Ï† : â„• â†’ â„•, strict_mono Ï† âˆ§ strict_mono (u âˆ˜ Ï†) :=
strict_mono_subseq_of_tendsto_at_top (tendsto_at_top_mono _ hu tendsto_id)

lemma nat.id_le_of_strict_mono {Ï† : â„• â†’ â„•} (h : strict_mono Ï†) : âˆ€ n, n â‰¤ Ï† n :=
Î» n, nat.rec_on n (nat.zero_le _) (Î» n hn, nat.succ_le_of_lt (lt_of_le_of_lt hn $ h $ nat.lt_succ_self n))

lemma strict_mono.tendsto_at_top {Ï† : â„• â†’ â„•} (h : strict_mono Ï†) :
  tendsto Ï† at_top at_top :=
tendsto_at_top_mono _ (nat.id_le_of_strict_mono h) tendsto_id

lemma subseq_tendsto_of_countable_basis {X : Type*} {f : filter X} (hf : has_countable_basis f)
  {u : â„• â†’ X}
  (hx : map u at_top âŠ“ f â‰  âŠ¥) :
  âˆƒ (Î¸ : â„• â†’ â„•), (strict_mono Î¸) âˆ§ (tendsto (u âˆ˜ Î¸) at_top f) :=
begin
  let B := decreasing_enumerated_basis.of_has_countable_basis hf,
  have : âˆ€ N, âˆƒ n â‰¥ N, u n âˆˆ B N,
    from Î» N, filter.map_at_top_inf_ne_bot_iff.mp hx _ (B.basis_mem N) N,
  choose Ï† hÏ† using this,
  cases forall_and_distrib.mp hÏ† with Ï†_ge Ï†_in,
  have lim_uÏ† : tendsto (u âˆ˜ Ï†) at_top f,
    from B.tendsto' Ï†_in,
  have lim_Ï† : tendsto Ï† at_top at_top,
    from (tendsto_at_top_mono _ Ï†_ge tendsto_id),
  obtain âŸ¨Ïˆ, hÏˆ, hÏˆÏ†âŸ© : âˆƒ Ïˆ : â„• â†’ â„•, strict_mono Ïˆ âˆ§ strict_mono (Ï† âˆ˜ Ïˆ),
    from strict_mono_subseq_of_tendsto_at_top lim_Ï†,
  exact âŸ¨Ï† âˆ˜ Ïˆ, hÏˆÏ†, lim_uÏ†.comp hÏˆ.tendsto_at_topâŸ©,
end

open topological_space

variables {X : Type*} [topological_space X] [first_countable_topology X]

lemma first_countable_topology.tendsto_subseq
  {u : â„• â†’ X} {x : X} (hx : map u at_top âŠ“ ð“ x â‰  âŠ¥) :
  âˆƒ (Ïˆ : â„• â†’ â„•), (strict_mono Ïˆ) âˆ§ (u âˆ˜ Ïˆ âŸ¶ x) :=
subseq_tendsto_of_countable_basis (first_countable_topology.nhds_generated_countable x) hx

lemma compact.tendsto_subseq' {s : set X} {u : â„• â†’ X} (hs : compact s) (hu : âˆ€á¶  n in at_top, u n âˆˆ s) :
âˆƒ (x âˆˆ s) (Ï† : â„• â†’ â„•), strict_mono Ï† âˆ§ tendsto (u âˆ˜ Ï†) at_top (ð“ x) :=
begin
  rcases hs (map u at_top) (map_ne_bot $ at_top_ne_bot) (le_principal_iff.mpr hu) with âŸ¨x, x_in, hxâŸ©,
  exact âŸ¨x, x_in, first_countable_topology.tendsto_subseq hxâŸ©,
end

lemma compact.tendsto_subseq {s : set X} {u : â„• â†’ X} (hs : compact s) (hu : âˆ€ n, u n âˆˆ s) :
âˆƒ (x âˆˆ s) (Ï† : â„• â†’ â„•), strict_mono Ï† âˆ§ tendsto (u âˆ˜ Ï†) at_top (ð“ x) :=
hs.tendsto_subseq' (univ_mem_sets' hu)
