/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Author: Yury Kudryashov, Johannes HÃ¶lzl, Mario Carneiro, Patrick Massot
-/

import order.filter.basic data.set.countable

/-! # Filter bases

In this file we define `filter.has_basis l p s`, where `l` is a filter on `Î±`, `p` is a predicate
on some index set `Î¹`, and `s : Î¹ â†’ set Î±`.

## Main statements

* `has_basis.mem_iff`, `has_basis.mem_of_superset`, `has_basis.mem_of_mem` : restate `t âˆˆ f` in terms
  of a basis;
* `basis_sets` : all sets of a filter form a basis;
* `has_basis.inf`, `has_basis.inf_principal`, `has_basis.prod`, `has_basis.prod_self`,
  `has_basis.map`, `has_basis.comap` : combinators to construct filters of `l âŠ“ l'`,
  `l âŠ“ principal t`, `l.prod l'`, `l.prod l`, `l.map f`, `l.comap f` respectively;
* `has_basis.le_iff`, `has_basis.ge_iff`, has_basis.le_basis_iff` : restate `l â‰¤ l'` in terms
  of bases.
* `has_basis.tendsto_right_iff`, `has_basis.tendsto_left_iff`, `has_basis.tendsto_iff` : restate
  `tendsto f l l'` in terms of bases.

## Implementation notes

As with `Union`/`bUnion`/`sUnion`, there are three different approaches to filter bases:

* `has_basis l s`, `s : set (set Î±)`;
* `has_basis l s`, `s : Î¹ â†’ set Î±`;
* `has_basis l p s`, `p : Î¹ â†’ Prop`, `s : Î¹ â†’ set Î±`.

We use the latter one because, e.g., `ğ“ x` in an `emetric_space` or in a `metric_space` has a basis
of this form. The other two can be emulated using `s = id` or `p = Î» _, true`.

With this approach sometimes one needs to `simp` the statement provided by the `has_basis`
machinery, e.g., `simp only [exists_prop, true_and]` or `simp only [forall_const]` can help
with the case `p = Î» _, true`.
-/

namespace filter
variables {Î± : Type*} {Î² : Type*} {Î³ : Type*} {Î¹ : Type*} {Î¹' : Type*}

open set

/-- We say that a filter `l` has a basis `s : Î¹ â†’ set Î±` bounded by `p : Î¹ â†’ Prop`,
if `t âˆˆ l` if and only if `t` includes `s i` for some `i` such that `p i`. -/
protected def has_basis (l : filter Î±) (p : Î¹ â†’ Prop) (s : Î¹ â†’ set Î±) : Prop :=
âˆ€ t : set Î±, t âˆˆ l â†” âˆƒ i (hi : p i), s i âŠ† t

section same_type

variables {l l' : filter Î±} {p : Î¹ â†’ Prop} {s : Î¹ â†’ set Î±} {t : set Î±} {i : Î¹}
  {p' : Î¹' â†’ Prop} {s' : Î¹' â†’ set Î±} {i' : Î¹'}

/-- Definition of `has_basis` unfolded to make it useful for `rw` and `simp`. -/
lemma has_basis.mem_iff (hl : l.has_basis p s) : t âˆˆ l â†” âˆƒ i (hi : p i), s i âŠ† t :=
hl t

lemma has_basis.eventually_iff (hl : l.has_basis p s) {q : Î± â†’ Prop} :
  (âˆ€á¶  x in l, q x) â†” âˆƒ i (hi : p i), âˆ€ â¦ƒxâ¦„, x âˆˆ s i â†’ q x :=
hl _

lemma has_basis.mem_of_superset (hl : l.has_basis p s) (hi : p i) (ht : s i âŠ† t) : t âˆˆ l :=
(hl t).2 âŸ¨i, hi, htâŸ©

lemma has_basis.mem_of_mem (hl : l.has_basis p s) (hi : p i) : s i âˆˆ l :=
hl.mem_of_superset hi $ subset.refl _

lemma has_basis.forall_nonempty_iff_ne_bot (hl : l.has_basis p s) :
  (âˆ€ {i}, p i â†’ (s i).nonempty) â†” l â‰  âŠ¥ :=
âŸ¨Î» H, forall_sets_nonempty_iff_ne_bot.1 $
  Î» s hs, let âŸ¨i, hi, hisâŸ© := (hl s).1 hs in (H hi).mono his,
  Î» H i hi, nonempty_of_mem_sets H (hl.mem_of_mem hi)âŸ©

lemma basis_sets (l : filter Î±) : l.has_basis (Î» s : set Î±, s âˆˆ l) id :=
Î» t, exists_sets_subset_iff.symm

lemma at_top_basis [nonempty Î±] [semilattice_sup Î±] :
  (@at_top Î± _).has_basis (Î» _, true) Ici :=
Î» t, by simpa only [exists_prop, true_and] using @mem_at_top_sets Î± _ _ t

lemma at_top_basis' [semilattice_sup Î±] (a : Î±) :
  (@at_top Î± _).has_basis (Î» x, a â‰¤ x) Ici :=
Î» t, (@at_top_basis Î± âŸ¨aâŸ© _ t).trans
  âŸ¨Î» âŸ¨x, _, hxâŸ©, âŸ¨x âŠ” a, le_sup_right, Î» y hy, hx (le_trans le_sup_left hy)âŸ©,
    Î» âŸ¨x, _, hxâŸ©, âŸ¨x, trivial, hxâŸ©âŸ©

theorem has_basis.ge_iff (hl' : l'.has_basis p' s')  : l â‰¤ l' â†” âˆ€ i', p' i' â†’ s' i' âˆˆ l :=
âŸ¨Î» h i' hi', h $ hl'.mem_of_mem hi',
  Î» h s hs, let âŸ¨i', hi', hsâŸ© := (hl' s).1 hs in mem_sets_of_superset (h _ hi') hsâŸ©

theorem has_basis.le_iff (hl : l.has_basis p s) : l â‰¤ l' â†” âˆ€ t âˆˆ l', âˆƒ i (hi : p i), s i âŠ† t :=
by simp only [le_def, hl.mem_iff]

theorem has_basis.le_basis_iff (hl : l.has_basis p s) (hl' : l'.has_basis p' s') :
  l â‰¤ l' â†” âˆ€ i', p' i' â†’ âˆƒ i (hi : p i), s i âŠ† s' i' :=
by simp only [hl'.ge_iff, hl.mem_iff]

lemma has_basis.inf (hl : l.has_basis p s) (hl' : l'.has_basis p' s') :
  (l âŠ“ l').has_basis (Î» i : Î¹ Ã— Î¹', p i.1 âˆ§ p' i.2) (Î» i, s i.1 âˆ© s' i.2) :=
begin
  intro t,
  simp only [mem_inf_sets, exists_prop, hl.mem_iff, hl'.mem_iff],
  split,
  { rintros âŸ¨t, âŸ¨i, hi, htâŸ©, t', âŸ¨i', hi', ht'âŸ©, HâŸ©,
    use [(i, i'), âŸ¨hi, hi'âŸ©, subset.trans (inter_subset_inter ht ht') H] },
  { rintros âŸ¨âŸ¨i, i'âŸ©, âŸ¨hi, hi'âŸ©, HâŸ©,
    use [s i, i, hi, subset.refl _, s' i', i', hi', subset.refl _, H] }
end

lemma has_basis.inf_principal (hl : l.has_basis p s) (s' : set Î±) :
  (l âŠ“ principal s').has_basis p (Î» i, s i âˆ© s') :=
Î» t, by simp only [mem_inf_principal, hl.mem_iff, subset_def, mem_set_of_eq,
  mem_inter_iff, and_imp]

lemma has_basis.eq_binfi (h : l.has_basis p s) :
  l = â¨… i (_ : p i), principal (s i) :=
eq_binfi_of_mem_sets_iff_exists_mem $ Î» t, by simp only [h.mem_iff, mem_principal_sets]

lemma has_basis.eq_infi (h : l.has_basis (Î» _, true) s) :
  l = â¨… i, principal (s i) :=
by simpa only [infi_true] using h.eq_binfi

@[nolint ge_or_gt] -- see Note [nolint_ge]
lemma has_basis_infi_principal {s : Î¹ â†’ set Î±} (h : directed (â‰¥) s) (ne : nonempty Î¹) :
  (â¨… i, principal (s i)).has_basis (Î» _, true) s :=
begin
  refine Î» t, (mem_infi (h.mono_comp _ _) ne t).trans $
    by simp only [exists_prop, true_and, mem_principal_sets],
  exact Î» _ _, principal_mono.2
end

@[nolint ge_or_gt] -- see Note [nolint_ge]
lemma has_basis_binfi_principal {s : Î² â†’ set Î±} {S : set Î²} (h : directed_on (s â»Â¹'o (â‰¥)) S)
  (ne : S.nonempty) :
  (â¨… i âˆˆ S, principal (s i)).has_basis (Î» i, i âˆˆ S) s :=
begin
  refine Î» t, (mem_binfi _ ne).trans $ by simp only [mem_principal_sets],
  rw [directed_on_iff_directed, â† directed_comp, (âˆ˜)] at h âŠ¢,
  apply h.mono_comp _ _,
  exact Î» _ _, principal_mono.2
end

lemma has_basis.map (f : Î± â†’ Î²) (hl : l.has_basis p s) :
  (l.map f).has_basis p (Î» i, f '' (s i)) :=
Î» t, by simp only [mem_map, image_subset_iff, hl.mem_iff, preimage]

lemma has_basis.comap (f : Î² â†’ Î±) (hl : l.has_basis p s) :
  (l.comap f).has_basis p (Î» i, f â»Â¹' (s i)) :=
begin
  intro t,
  simp only [mem_comap_sets, exists_prop, hl.mem_iff],
  split,
  { rintros âŸ¨t', âŸ¨i, hi, ht'âŸ©, HâŸ©,
    exact âŸ¨i, hi, subset.trans (preimage_mono ht') HâŸ© },
  { rintros âŸ¨i, hi, HâŸ©,
    exact âŸ¨s i, âŸ¨i, hi, subset.refl _âŸ©, HâŸ© }
end

lemma has_basis.prod_self (hl : l.has_basis p s) :
  (l.prod l).has_basis p (Î» i, (s i).prod (s i)) :=
begin
  intro t,
  apply mem_prod_iff.trans,
  split,
  { rintros âŸ¨tâ‚, htâ‚, tâ‚‚, htâ‚‚, HâŸ©,
    rcases hl.mem_iff.1 (inter_mem_sets htâ‚ htâ‚‚) with âŸ¨i, hi, htâŸ©,
    exact âŸ¨i, hi, Î» p âŸ¨hpâ‚, hpâ‚‚âŸ©, H âŸ¨(ht hpâ‚).1, (ht hpâ‚‚).2âŸ©âŸ© },
  { rintros âŸ¨i, hi, HâŸ©,
    exact âŸ¨s i, hl.mem_of_mem hi, s i, hl.mem_of_mem hi, HâŸ© }
end

@[nolint has_inhabited_instance]
structure decreasing_enumerated_basis (f : filter Î±)  :=
(V : â„• â†’ set Î±)
(decreasing : âˆ€ {n m}, n â‰¤ m â†’ V m âŠ† V n)
(has_basis : filter.has_basis f (Î» _, true) V)

instance  {Î± : Type*} {f : filter Î±} : has_coe_to_fun (decreasing_enumerated_basis f) :=
âŸ¨_, decreasing_enumerated_basis.VâŸ©

lemma decreasing_enumerated_basis.basis {f : filter Î±} (B : decreasing_enumerated_basis f) :
  âˆ€ s, s âˆˆ f â†” âˆƒ n, B n âŠ† s :=
Î» s, by simpa [B.has_basis.mem_iff]

lemma decreasing_enumerated_basis.basis_mem {f : filter Î±} (B : decreasing_enumerated_basis f) :
  âˆ€ n, B n âˆˆ f :=
Î» n, B.has_basis.mem_of_mem trivial
end same_type

section two_types

variables {la : filter Î±} {pa : Î¹ â†’ Prop} {sa : Î¹ â†’ set Î±}
  {lb : filter Î²} {pb : Î¹' â†’ Prop} {sb : Î¹' â†’ set Î²} {f : Î± â†’ Î²}

lemma has_basis.tendsto_left_iff (hla : la.has_basis pa sa) :
  tendsto f la lb â†” âˆ€ t âˆˆ lb, âˆƒ i (hi : pa i), âˆ€ x âˆˆ sa i, f x âˆˆ t :=
by { simp only [tendsto, (hla.map f).le_iff, image_subset_iff], refl }

lemma has_basis.tendsto_right_iff (hlb : lb.has_basis pb sb) :
  tendsto f la lb â†” âˆ€ i (hi : pb i), âˆ€á¶  x in la, f x âˆˆ sb i :=
by simp only [tendsto, hlb.ge_iff, mem_map, filter.eventually]

lemma has_basis.tendsto_iff (hla : la.has_basis pa sa) (hlb : lb.has_basis pb sb) :
  tendsto f la lb â†” âˆ€ ib (hib : pb ib), âˆƒ ia (hia : pa ia), âˆ€ x âˆˆ sa ia, f x âˆˆ sb ib :=
by simp only [hlb.tendsto_right_iff, hla.eventually_iff, subset_def, mem_set_of_eq]

lemma tendsto.basis_left (H : tendsto f la lb) (hla : la.has_basis pa sa) :
  âˆ€ t âˆˆ lb, âˆƒ i (hi : pa i), âˆ€ x âˆˆ sa i, f x âˆˆ t :=
hla.tendsto_left_iff.1 H

lemma tendsto.basis_right (H : tendsto f la lb) (hlb : lb.has_basis pb sb) :
  âˆ€ i (hi : pb i), âˆ€á¶  x in la, f x âˆˆ sb i :=
hlb.tendsto_right_iff.1 H

lemma tendsto.basis_both (H : tendsto f la lb) (hla : la.has_basis pa sa)
  (hlb : lb.has_basis pb sb) :
  âˆ€ ib (hib : pb ib), âˆƒ ia (hia : pa ia), âˆ€ x âˆˆ sa ia, f x âˆˆ sb ib :=
(hla.tendsto_iff hlb).1 H

lemma has_basis.prod (hla : la.has_basis pa sa) (hlb : lb.has_basis pb sb) :
  (la.prod lb).has_basis (Î» i : Î¹ Ã— Î¹', pa i.1 âˆ§ pb i.2) (Î» i, (sa i.1).prod (sb i.2)) :=
(hla.comap prod.fst).inf (hlb.comap prod.snd)

lemma decreasing_enumerated_basis.tendsto {f : filter Î±} {g : filter Î²}
(B : decreasing_enumerated_basis g) (Ï† : Î± â†’ Î²) : tendsto Ï† f g â†” âˆ€ n, âˆ€á¶  x in f, Ï† x âˆˆ B n :=
by simpa [B.has_basis.tendsto_right_iff]

lemma decreasing_enumerated_basis.tendsto' {g : filter Î²}
(B : decreasing_enumerated_basis g) {Ï† : â„• â†’ Î²} (h : âˆ€ n, Ï† n âˆˆ B n) : tendsto Ï† at_top g :=
begin
  rw B.tendsto,
  intro n,
  rw eventually_at_top,
  use n,
  intros m hm,
  exact B.decreasing hm (h m),
end

end two_types

/--
A filter has a countable basis iff it is generated by a countable collection
of subsets of Î±. (A filter is a generated by a collection of sets iff it is
the infimum of the principal filters.)

Note: we do not require the collection to be closed under finite intersections.
-/
def has_countable_basis (f : filter Î±) : Prop :=
âˆƒ s : set (set Î±), countable s âˆ§ f = â¨… t âˆˆ s, principal t

lemma has_countable_basis_of_seq (f : filter Î±) (x : â„• â†’ set Î±) (h : f = â¨… i, principal (x i)) :
  f.has_countable_basis :=
âŸ¨range x, countable_range _, by rwa infi_rangeâŸ©

lemma seq_of_has_countable_basis (f : filter Î±) (cblb : f.has_countable_basis) :
    âˆƒ x : â„• â†’ set Î±, f = â¨… i, principal (x i) :=
begin
  rcases cblb with âŸ¨B, Bcbl, genâŸ©, subst gen,
  cases B.eq_empty_or_nonempty with hB Bnonempty, { use Î» n, set.univ, simp [principal_univ, *] },
  rw countable_iff_exists_surjective_to_subtype Bnonempty at Bcbl,
  rcases Bcbl with âŸ¨g, gsurjâŸ©,
  rw infi_subtype',
  use (Î» n, g n), apply le_antisymm; rw le_infi_iff,
  { intro i, apply infi_le_of_le (g i) _, apply le_refl _ },
  { intros a, rcases gsurj a with i, apply infi_le_of_le i _, subst h, apply le_refl _ }
end

/--
Different characterization of countable basis. A filter has a countable basis
iff it is generated by a sequence of sets.
-/
lemma has_countable_basis_iff_seq (f : filter Î±) :
  f.has_countable_basis â†”
    âˆƒ x : â„• â†’ set Î±, f = â¨… i, principal (x i) :=
âŸ¨seq_of_has_countable_basis _, Î» âŸ¨x, xgenâŸ©, has_countable_basis_of_seq _ x xgenâŸ©

lemma mono_seq_of_has_countable_basis (f : filter Î±) (cblb : f.has_countable_basis) :
  âˆƒ x : â„• â†’ set Î±, (âˆ€ i j, i â‰¤ j â†’ x j âŠ† x i) âˆ§ f = â¨… i, principal (x i) :=
begin
  rcases (seq_of_has_countable_basis f cblb) with âŸ¨x', hx'âŸ©,
  let x := Î» n, â‹‚ m â‰¤ n, x' m,
  use x, split,
  { intros i j hij a, simp [x], intros h i' hi'i, apply h, transitivity; assumption },
  subst hx', apply le_antisymm; rw le_infi_iff; intro i,
  { rw le_principal_iff, apply Inter_mem_sets (finite_le_nat _),
    intros j hji, rw â† le_principal_iff, apply infi_le_of_le j _, apply le_refl _ },
  { apply infi_le_of_le i _, rw principal_mono, intro a, simp [x], intro h, apply h, refl },
end

/--
Different characterization of countable basis. A filter has a countable basis
iff it is generated by a monotonically decreasing sequence of sets.
-/
lemma has_countable_basis_iff_mono_seq (f : filter Î±) :
  f.has_countable_basis â†”
    âˆƒ x : â„• â†’ set Î±, (âˆ€ i j, i â‰¤ j â†’ x j âŠ† x i) âˆ§ f = â¨… i, principal (x i) :=
âŸ¨mono_seq_of_has_countable_basis _, Î» âŸ¨x, _, xgenâŸ©, has_countable_basis_of_seq _ x xgenâŸ©

/--
Different characterization of countable basis. A filter has a countable basis
iff there exists a monotonically decreasing sequence of sets `x i`
such that `s âˆˆ f â†” âˆƒ i, x i âŠ† s`. -/
lemma has_countable_basis_iff_mono_seq' (f : filter Î±) :
  f.has_countable_basis â†”
    âˆƒ x : â„• â†’ set Î±, (âˆ€ i j, i â‰¤ j â†’ x j âŠ† x i) âˆ§ (âˆ€ {s}, s âˆˆ f â†” âˆƒ i, x i âŠ† s) :=
begin
  refine (has_countable_basis_iff_mono_seq f).trans (exists_congr $ Î» x, and_congr_right _),
  intro hmono,
  have : directed (â‰¥) (Î» i, principal (x i)),
    from directed_of_mono _ (Î» i j hij, principal_mono.2 (hmono _ _ hij)),
  simp only [filter.ext_iff, mem_infi this âŸ¨0âŸ©, mem_Union, mem_principal_sets]
end

lemma has_countable_basis.comap {l : filter Î²} (h : has_countable_basis l) (f : Î± â†’ Î²) :
  has_countable_basis (l.comap f) :=
begin
  rcases h with âŸ¨S, hâ‚, hâ‚‚âŸ©,
  refine âŸ¨preimage f '' S, countable_image _ hâ‚, _âŸ©,
  calc comap f l = â¨… s âˆˆ S, principal (f â»Â¹' s) : by simp [hâ‚‚]
  ... = â¨… s âˆˆ S, â¨… (t : set Î±) (H : f â»Â¹' s = t), principal t : by simp
  ... = â¨… (t : set Î±) (s âˆˆ S) (hâ‚‚ : f â»Â¹' s = t), principal t :
    by { rw [infi_comm], congr' 1, ext t, rw [infi_comm] }
  ... = _ : by simp [-infi_infi_eq_right, infi_and]
end

-- TODO : prove this for a encodable type
lemma has_countable_basis_at_top_finset_nat : has_countable_basis (@at_top (finset â„•) _) :=
begin
  refine has_countable_basis_of_seq _ (Î»N, Ici (finset.range N)) (eq_infi_of_mem_sets_iff_exists_mem _),
  assume s,
  rw mem_at_top_sets,
  refine âŸ¨_, Î» âŸ¨N, hNâŸ©, âŸ¨finset.range N, hNâŸ©âŸ©,
  rintros âŸ¨t, htâŸ©,
  rcases mem_at_top_sets.1 (tendsto_finset_range (mem_at_top t)) with âŸ¨N, hNâŸ©,
  simp only [preimage, mem_set_of_eq] at hN,
  exact âŸ¨N, mem_principal_sets.2 $ Î» t' ht', ht t' $ le_trans (hN _ $ le_refl N) ht'âŸ©
end

lemma has_countable_basis.tendsto_iff_seq_tendsto {f : Î± â†’ Î²} {k : filter Î±} {l : filter Î²}
  (hcb : k.has_countable_basis) :
  tendsto f k l â†” (âˆ€ x : â„• â†’ Î±, tendsto x at_top k â†’ tendsto (f âˆ˜ x) at_top l) :=
suffices (âˆ€ x : â„• â†’ Î±, tendsto x at_top k â†’ tendsto (f âˆ˜ x) at_top l) â†’ tendsto f k l,
  from âŸ¨by intros; apply tendsto.comp; assumption, by assumptionâŸ©,
begin
  rw filter.has_countable_basis_iff_mono_seq at hcb,
  rcases hcb with âŸ¨g, gmon, gbasisâŸ©,
  have gbasis : âˆ€ A, A âˆˆ k â†” âˆƒ i, g i âŠ† A,
  { intro A,
    subst gbasis,
    rw mem_infi,
    { simp only [set.mem_Union, iff_self, filter.mem_principal_sets] },
    { exact directed_of_mono _ (Î» i j h, principal_mono.mpr $ gmon _ _ h) },
    { apply_instance } },
  classical, contrapose,
  simp only [not_forall, not_imp, not_exists, subset_def, @tendsto_def _ _ f, gbasis],
  rintro âŸ¨B, hBl, hfBkâŸ©,
  choose x h using hfBk,
  use x, split,
  { simp only [tendsto_at_top', gbasis],
    rintros A âŸ¨i, hgiAâŸ©,
    use i,
    refine (Î» j hj, hgiA $ gmon _ _ hj _),
    simp only [h] },
  { simp only [tendsto_at_top', (âˆ˜), not_forall, not_exists],
    use [B, hBl],
    intro i, use [i, (le_refl _)],
    apply (h i).right },
end

lemma has_countable_basis.tendsto_of_seq_tendsto {f : Î± â†’ Î²} {k : filter Î±} {l : filter Î²}
  (hcb : k.has_countable_basis) :
  (âˆ€ x : â„• â†’ Î±, tendsto x at_top k â†’ tendsto (f âˆ˜ x) at_top l) â†’ tendsto f k l :=
hcb.tendsto_iff_seq_tendsto.2

def decreasing_enumerated_basis.of_has_countable_basis {f : filter Î±}
  (hf : has_countable_basis f) : decreasing_enumerated_basis f :=
begin
  choose V hV using mono_seq_of_has_countable_basis f hf,
  cases hV with dec basis,
  refine { V := V, decreasing := dec, ..},
  have dir : directed ge (Î» (i : â„•), principal (V i)),
  { intros i j,
    use max i j,
    simp only [mem_principal_sets, ge_iff_le, le_principal_iff],
    split ; apply dec ; [exact le_max_left _ _, exact le_max_right _ _] },
  intros S,
  simp [basis, mem_infi dir],
end
end filter

